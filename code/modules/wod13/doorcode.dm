#define LOCKDIFFICULTY_1 1
#define LOCKTIMER_1 4 SECONDS
#define LOCKDIFFICULTY_2 2
#define LOCKTIMER_2 5 SECONDS
#define LOCKDIFFICULTY_3 3
#define LOCKTIMER_3 6 SECONDS
#define LOCKDIFFICULTY_4 4
#define LOCKTIMER_4 7 SECONDS
#define LOCKDIFFICULTY_5 5
#define LOCKTIMER_5 8 SECONDS
#define LOCKDIFFICULTY_6 6 //originally should have been 10, but that wouldn't work unless locktimer is explicitly declared beforehand, which it won't be
#define LOCKTIMER_6 9 SECONDS
#define LOCKDIFFICULTY_7 7
#define LOCKTIMER_7 10 SECONDS

/obj/item/vamp/keys
	name = "\improper keys"
	desc = "Those can open some doors."
	icon = 'code/modules/wod13/items.dmi'
	icon_state = "keys"
	item_flags = NOBLUDGEON
	w_class = WEIGHT_CLASS_TINY
	armor = list(MELEE = 0, BULLET = 0, LASER = 0, ENERGY = 0, BOMB = 0, BIO = 0, RAD = 0, FIRE = 100, ACID = 100)
	resistance_flags = FIRE_PROOF | ACID_PROOF
	onflooricon = 'code/modules/wod13/onfloor.dmi'

	var/list/accesslocks = list(
		"nothing"
	)
	var/roundstart_fix = FALSE


/obj/structure/vampdoor
	name = "\improper door"
	desc = "It opens and closes."
	icon = 'code/modules/wod13/doors.dmi'
	icon_state = "door-1"
	plane = GAME_PLANE
	layer = ABOVE_ALL_MOB_LAYER
	pixel_w = -16
	anchored = TRUE
	density = TRUE
	opacity = TRUE
	resistance_flags = INDESTRUCTIBLE | LAVA_PROOF | FIRE_PROOF | UNACIDABLE | ACID_PROOF | FREEZE_PROOF

	var/baseicon = "door"

	var/magic_lock = FALSE
	var/closed = TRUE
	var/locked = FALSE
	var/lock_id = "nothing"
	var/glass = FALSE
	var/hacking = FALSE
	var/lockpick_timer = 5 SECONDS //[Lucifernix] - Never have the lockpick timer lower than 7. At 7 it will unlock instantly!!
	var/lockpick_difficulty = 2

	var/open_sound = 'code/modules/wod13/sounds/door_open.ogg'
	var/close_sound = 'code/modules/wod13/sounds/door_close.ogg'
	var/lock_sound = 'code/modules/wod13/sounds/door_locked.ogg'
	var/burnable = FALSE


/obj/structure/vampdoor/New()
	..()
	switch(lockpick_difficulty) //This is fine because any overlap gets intercepted before
		if(LOCKDIFFICULTY_7 to INFINITY)
			lockpick_timer = LOCKTIMER_7
		if(LOCKDIFFICULTY_6 to LOCKDIFFICULTY_7)
			lockpick_timer = LOCKTIMER_6
		if(LOCKDIFFICULTY_5 to LOCKDIFFICULTY_6)
			lockpick_timer = LOCKTIMER_5
		if(LOCKDIFFICULTY_4 to LOCKDIFFICULTY_5)
			lockpick_timer = LOCKTIMER_4
		if(LOCKDIFFICULTY_3 to LOCKDIFFICULTY_4)
			lockpick_timer = LOCKTIMER_3
		if(LOCKDIFFICULTY_2 to LOCKDIFFICULTY_3)
			lockpick_timer = LOCKTIMER_2
		if(-INFINITY to LOCKDIFFICULTY_2) //LOCKDIFFICULTY_1 is basically the minimum so we can just do LOCKTIMER_1 from -INFINITY
			lockpick_timer = LOCKTIMER_1

/obj/structure/vampdoor
	var/last_investigation = 0

/obj/structure/vampdoor/examine(mob/user)
	. = ..()
	if(!ishuman(user))
		return
	var/mob/living/carbon/human/H = user
	if(!H.is_holding_item_of_type(/obj/item/vamp/keys/hack))
		return
	var/message //So the code isn't flooded with . +=, it's just a visual thing
	var/difference = max(get_a_wits(user), get_a_dexterity(user))+get_a_security(user) - lockpick_difficulty //Lower number = higher difficulty
	switch(difference) //Because rand(1,20) always adds a minimum of 1 we take that into consideration for our theoretical roll ranges, which really makes it a random range of 19.
		if(-INFINITY to -1) //Roll can never go above 10 (-11 + 20 = 9), impossible to lockpick.
			message = "<span class='warning'>You don't have any chance of lockpicking this with your current skills!</span>"
		if(0 to 1)
			message = "<span class='warning'>This door looks extremely complicated. You figure you will have to be lucky to break it open."
		if(2 to 3)
			message = "<span class='notice'>This door looks very complicated. You might need a few tries to lockpick it."
		if(4 to 5) //Only 3 numbers here instead of 4.
			message = "<span class='notice'>This door looks mildly complicated. It shouldn't be too hard to lockpick it.</span>"
		if(6 to 7) //Impossible to break the lockpick from here on because minimum rand(1,20) will always move the value to 2.
			message = "<span class='nicegreen'>This door is somewhat simple. It should be pretty easy for you to lockpick it.</span>"
		if(8 to INFINITY) //Becomes guaranteed to lockpick at 9.
			message = "<span class='nicegreen'>This door is really simple to you. It should be very easy to lockpick it.</span>"
	. += "[message]"
	if(isliving(user) && last_investigation <= world.time)
		last_investigation = world.time+30 SECONDS
		var/investigation_check = secret_vampireroll(get_a_intelligence(user)+get_a_investigation(user), 6, user)
		if(investigation_check >= 3)
			var/keys_that_can_open = ""
			for(var/i in subtypesof(/obj/item/vamp/keys))
				var/obj/item/vamp/keys/K = i
				if(lock_id in initial(K.accesslocks))
					if(keys_that_can_open == "")
						keys_that_can_open += initial(K.name)
					else
						keys_that_can_open += ", [initial(K.name)]"
			to_chat(user, "You can open that door with: [keys_that_can_open]")

/obj/structure/vampdoor/attack_hand(mob/user)
	. = ..()
	var/mob/living/N = user
	if(locked)
		if(N.a_intent != INTENT_HARM)
			playsound(src, lock_sound, 75, TRUE)
			to_chat(user, "<span class='warning'>[src] is locked!</span>")
		else
			if(ishuman(user))
				var/mob/living/carbon/human/H = user
				if(get_potence_dices(H) > 0)
					if((get_potence_dices(H) * 2) >= lockpick_difficulty)
						playsound(get_turf(src), 'code/modules/wod13/sounds/get_bent.ogg', 100, FALSE)
						var/obj/item/shield/door/D = new(get_turf(src))
						D.icon_state = baseicon
						var/atom/throw_target = get_edge_target_turf(src, user.dir)
						D.throw_at(throw_target, rand(2, 4), 4, user)
						qdel(src)
					else
						pixel_z = pixel_z+rand(-1, 1)
						pixel_w = pixel_w+rand(-1, 1)
						playsound(get_turf(src), 'code/modules/wod13/sounds/get_bent.ogg', 50, TRUE)
						to_chat(user, "<span class='warning'>[src] is locked, and you aren't strong enough to break it down!</span>")
						spawn(2)
							pixel_z = initial(pixel_z)
							pixel_w = initial(pixel_w)
				else
					pixel_z = pixel_z+rand(-1, 1)
					pixel_w = pixel_w+rand(-1, 1)
					playsound(src, 'code/modules/wod13/sounds/knock.ogg', 75, TRUE)
					to_chat(user, "<span class='warning'>[src] is locked!</span>")
					spawn(2)
						pixel_z = initial(pixel_z)
						pixel_w = initial(pixel_w)
		return

	if(closed)
		playsound(src, open_sound, 75, TRUE)
		icon_state = "[baseicon]-0"
		density = FALSE
		opacity = FALSE
		layer = OPEN_DOOR_LAYER
		to_chat(user, "<span class='notice'>You open [src].</span>")
		closed = FALSE
	else
		for(var/mob/living/L in src.loc)
			if(L)
				playsound(src, lock_sound, 75, TRUE)
				to_chat(user, "<span class='warning'>[L] is preventing you from closing [src].</span>")
				return
		playsound(src, close_sound, 75, TRUE)
		icon_state = "[baseicon]-1"
		density = TRUE
		if(!glass)
			opacity = TRUE
		layer = ABOVE_ALL_MOB_LAYER
		to_chat(user, "<span class='notice'>You close [src].</span>")
		closed = TRUE

/obj/structure/vampdoor/attackby(obj/item/W, mob/living/user, params)
	if(istype(W, /obj/item/melee/vampirearms/sledgehammer) && !hacking)
		hacking = TRUE
		for(var/mob/living/carbon/human/npc/police/P in oviewers(7, src))
			if(P)
				P.Aggro(user)
		if(do_after(user, round(lockpick_timer/2), src))
			hacking = FALSE
			playsound(get_turf(src), 'code/modules/wod13/sounds/get_bent.ogg', 100, FALSE)
			var/difficulties = secret_vampireroll(get_a_strength(user)+get_a_melee(user), lockpick_difficulty, user)
			if(difficulties == -1)
				user.visible_message("<span class='warning'>[user] fails to break [src] with [W]!</span>", \
							"<span class='userdanger'>You fail to break [src] with [W]!</span>")
				user.a_intent = INTENT_HARM
				user.AdjustKnockdown(60, TRUE)
				return
			else if(difficulties > 2)
				var/obj/item/shield/door/D = new(get_turf(src))
				D.icon_state = baseicon
				var/atom/throw_target = get_edge_target_turf(src, user.dir)
				D.throw_at(throw_target, rand(2, 4), 4, user)
				qdel(src)
			else
				animate(src, pixel_x = 16*sin(get_angle_raw(user.x, user.y, 0, 0, x, y, 0, 0)), pixel_y = 16*cos(get_angle_raw(user.x, user.y, 0, 0, x, y, 0, 0)), time = 5, loop = 1)
				animate(src, pixel_x = 0, pixel_y = 0)
				user.visible_message("<span class='warning'>[user] fails to break [src] with [W]!</span>", \
							"<span class='userdanger'>You fail to break [src] with [W]!</span>")
		else
			hacking = FALSE
	if(istype(W, /obj/item/vamp/keys/hack))
		if(locked)
			hacking = TRUE
			playsound(src, 'code/modules/wod13/sounds/hack.ogg', 100, TRUE)
			for(var/mob/living/carbon/human/npc/police/P in oviewers(7, src))
				if(P)
					P.Aggro(user)
			if(do_after(user, lockpick_timer, src))
				var/roll = secret_vampireroll(max(get_a_wits(user), get_a_dexterity(user))+get_a_security(user), lockpick_difficulty, user)
				if(roll == -1)
					to_chat(user, "<span class='warning'>Your lockpick broke!</span>")
					qdel(W)
					hacking = FALSE
				if(roll >= 3)
					to_chat(user, "<span class='notice'>You pick the lock.</span>")
					locked = FALSE
					hacking = FALSE
					return

				else
					to_chat(user, "<span class='warning'>You failed to pick the lock.</span>")
					hacking = FALSE
					return
			else
				to_chat(user, "<span class='warning'>You failed to pick the lock.</span>")
				hacking = FALSE
				return
		else
			if (closed) //yes, this is a thing you can extremely easily do in real life
				if(do_mob(user, src, lockpick_timer))
					var/roll = secret_vampireroll(max(get_a_wits(user), get_a_dexterity(user))+get_a_security(user), 5, user)
					if(roll == -1)
						to_chat(user, "<span class='warning'>Your lockpick broke!</span>")
						qdel(W)
					if(roll >= 3)
						to_chat(user, "<span class='notice'>You re-lock the door with your lockpick.</span>")
						locked = TRUE
						playsound(src, 'code/modules/wod13/sounds/hack.ogg', 100, TRUE)
						return
				return
	else if(istype(W, /obj/item/vamp/keys))
		var/obj/item/vamp/keys/KEY = W
		if(KEY.roundstart_fix)
			lock_id = pick(KEY.accesslocks)
			KEY.roundstart_fix = FALSE
		if(KEY.accesslocks)
			for(var/i in KEY.accesslocks)
				if(i == lock_id)
					if(!locked)
						playsound(src, lock_sound, 75, TRUE)
						to_chat(user, "[src] is now locked.")
						locked = TRUE
					else
						playsound(src, lock_sound, 75, TRUE)
						to_chat(user, "[src] is now unlocked.")
						locked = FALSE

/obj/structure/vampdoor/CanPass(atom/movable/mover, turf/target)
	. = ..()
	if(istype(mover, /mob/living/carbon/human))
		var/mob/living/carbon/human/H = mover
		if(HAS_TRAIT(H, TRAIT_PASSDOOR))
			return TRUE

#undef LOCKDIFFICULTY_1
#undef LOCKTIMER_1
#undef LOCKDIFFICULTY_2
#undef LOCKTIMER_2
#undef LOCKDIFFICULTY_3
#undef LOCKTIMER_3
#undef LOCKDIFFICULTY_4
#undef LOCKTIMER_4
#undef LOCKDIFFICULTY_5
#undef LOCKTIMER_5
#undef LOCKDIFFICULTY_6
#undef LOCKTIMER_6
#undef LOCKDIFFICULTY_7
#undef LOCKTIMER_7
