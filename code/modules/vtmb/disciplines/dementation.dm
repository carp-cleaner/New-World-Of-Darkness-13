/datum/discipline/dementation
	name = "Dementation"
	desc = "Makes all humans in radius mentally ill for a moment, supressing their defending ability."
	icon_state = "dementation"
	clan_restricted = TRUE
	power_type = /datum/discipline_power/dementation

/datum/discipline/dementation/post_gain()
	. = ..()
	owner.add_quirk(/datum/quirk/insanity)

/datum/discipline_power/dementation
	name = "Dementation power name"
	desc = "Dementation power description"

	vitae_cost = 0

	activate_sound = 'code/modules/wod13/sounds/insanity.ogg'

/datum/discipline_power/dementation/pre_activation_checks(mob/living/target)
	var/mypower = secret_vampireroll(get_a_wits(owner)+max(get_a_empathy(owner), get_a_intimidation(owner)), get_a_willpower(target), owner)
	if(mypower < 3)
		to_chat(owner, "<span class='warning'>You fail at corrupting!</span>")
		owner.emote("stare")
		if(mypower == -1)
			owner.Stun(3 SECONDS, TRUE)
			owner.do_jitter_animation(10)
		return FALSE
	return TRUE
//PASSION
/datum/discipline_power/dementation/passion
	name = "Passion"
	desc = "Stir the deepest parts of your target to manipulate their psyche."

	level = 1

	check_flags = DISC_CHECK_CAPABLE | DISC_CHECK_SPEAK
	target_type = TARGET_LIVING
	range = 7

	multi_activate = TRUE
	cooldown_length = 15 SECONDS
	duration_length = 3 SECONDS

/datum/discipline_power/dementation/passion/activate(mob/living/target)
	. = ..()
	if(iscarbon(target))
		var/mob/living/carbon/carbon_target = target
		carbon_target.remove_overlay(MUTATIONS_LAYER)
		var/mutable_appearance/dementation_overlay = mutable_appearance('code/modules/wod13/icons.dmi', "dementation", -MUTATIONS_LAYER)
		dementation_overlay.pixel_z = 1
		//what the fuck
		carbon_target.overlays_standing[MUTATIONS_LAYER] = dementation_overlay
		carbon_target.apply_overlay(MUTATIONS_LAYER)

	target.emote("laugh")
	to_chat(target, "<span class='userdanger'><b>HAHAHAHAHAHAHAHAHAHAHAHA!!</b></span>")
	owner.playsound_local(get_turf(target), pick('sound/items/SitcomLaugh1.ogg', 'sound/items/SitcomLaugh2.ogg', 'sound/items/SitcomLaugh3.ogg'), 100, FALSE)
	if(target.body_position == STANDING_UP)
		target.toggle_resting()

/datum/discipline_power/dementation/passion/deactivate(mob/living/target)
	. = ..()
	if(iscarbon(target))
		var/mob/living/carbon/carbon_target = target
		carbon_target.remove_overlay(MUTATIONS_LAYER)

//THE HAUNTING
/datum/discipline_power/dementation/the_haunting
	name = "The Haunting"
	desc = "Manipulate your target's senses, making them perceive what isn't there."

	level = 2

	check_flags = DISC_CHECK_CAPABLE | DISC_CHECK_SPEAK
	target_type = TARGET_LIVING
	range = 7

	multi_activate = TRUE
	cooldown_length = 15 SECONDS
	duration_length = 3 SECONDS

/datum/discipline_power/dementation/the_haunting/activate(mob/living/target)
	. = ..()
	if(iscarbon(target))
		var/mob/living/carbon/carbon_target = target
		carbon_target.remove_overlay(MUTATIONS_LAYER)
		var/mutable_appearance/dementation_overlay = mutable_appearance('code/modules/wod13/icons.dmi', "dementation", -MUTATIONS_LAYER)
		dementation_overlay.pixel_z = 1
		//what the fuck
		carbon_target.overlays_standing[MUTATIONS_LAYER] = dementation_overlay
		carbon_target.apply_overlay(MUTATIONS_LAYER)
		new /datum/hallucination/oh_yeah(carbon_target, TRUE)


	target.hallucination += 50

/datum/discipline_power/dementation/the_haunting/deactivate(mob/living/target)
	. = ..()
	if(iscarbon(target))
		var/mob/living/carbon/carbon_target = target
		carbon_target.remove_overlay(MUTATIONS_LAYER)

//EYES OF CHAOS
/datum/discipline_power/dementation/eyes_of_chaos
	name = "Eyes of Chaos"
	desc = "See the hidden patterns in the world and uncover people's true selves."

	level = 3

	check_flags = DISC_CHECK_CAPABLE | DISC_CHECK_SPEAK
	target_type = TARGET_LIVING
	range = 7

	multi_activate = TRUE
	cooldown_length = 15 SECONDS
	duration_length = 3 SECONDS

/datum/discipline_power/dementation/eyes_of_chaos/activate(mob/living/target)
	. = ..()
	if(iscarbon(target))
		var/mob/living/carbon/carbon_target = target
		carbon_target.remove_overlay(MUTATIONS_LAYER)
		var/mutable_appearance/dementation_overlay = mutable_appearance('code/modules/wod13/icons.dmi', "dementation", -MUTATIONS_LAYER)
		dementation_overlay.pixel_z = 1
		//what the fuck
		carbon_target.overlays_standing[MUTATIONS_LAYER] = dementation_overlay
		carbon_target.apply_overlay(MUTATIONS_LAYER)

	target.Immobilize(2 SECONDS)
	if(!HAS_TRAIT(target, TRAIT_KNOCKEDOUT) && !HAS_TRAIT(target, TRAIT_IMMOBILIZED) && !HAS_TRAIT(target, TRAIT_RESTRAINED))
		if(prob(50))
			dancefirst(target)
		else
			dancesecond(target)

/datum/discipline_power/dementation/eyes_of_chaos/deactivate(mob/living/target)
	. = ..()
	if(iscarbon(target))
		var/mob/living/carbon/carbon_target = target
		carbon_target.remove_overlay(MUTATIONS_LAYER)

/proc/dancefirst(mob/living/M)
	if(M.dancing)
		return
	M.dancing = TRUE
	var/matrix/initial_matrix = matrix(M.transform)
	for (var/i in 1 to 75)
		if (!M)
			return
		switch(i)
			if (1 to 15)
				initial_matrix = matrix(M.transform)
				initial_matrix.Translate(0,1)
				animate(M, transform = initial_matrix, time = 1, loop = 0)
			if (16 to 30)
				initial_matrix = matrix(M.transform)
				initial_matrix.Translate(1,-1)
				animate(M, transform = initial_matrix, time = 1, loop = 0)
			if (31 to 45)
				initial_matrix = matrix(M.transform)
				initial_matrix.Translate(-1,-1)
				animate(M, transform = initial_matrix, time = 1, loop = 0)
			if (46 to 60)
				initial_matrix = matrix(M.transform)
				initial_matrix.Translate(-1,1)
				animate(M, transform = initial_matrix, time = 1, loop = 0)
			if (61 to 75)
				initial_matrix = matrix(M.transform)
				initial_matrix.Translate(1,0)
				animate(M, transform = initial_matrix, time = 1, loop = 0)
		M.setDir(turn(M.dir, 90))
		switch (M.dir)
			if (NORTH)
				initial_matrix = matrix(M.transform)
				initial_matrix.Translate(0,3)
				animate(M, transform = initial_matrix, time = 1, loop = 0)
			if (SOUTH)
				initial_matrix = matrix(M.transform)
				initial_matrix.Translate(0,-3)
				animate(M, transform = initial_matrix, time = 1, loop = 0)
			if (EAST)
				initial_matrix = matrix(M.transform)
				initial_matrix.Translate(3,0)
				animate(M, transform = initial_matrix, time = 1, loop = 0)
			if (WEST)
				initial_matrix = matrix(M.transform)
				initial_matrix.Translate(-3,0)
				animate(M, transform = initial_matrix, time = 1, loop = 0)
		sleep(0.1 SECONDS)
	M.lying_fix()
	M.dancing = FALSE

/proc/dancesecond(mob/living/M)
	if(M.dancing)
		return
	M.dancing = TRUE
	animate(M, transform = matrix(180, MATRIX_ROTATE), time = 1, loop = 0)
	var/matrix/initial_matrix = matrix(M.transform)
	for (var/i in 1 to 60)
		if (!M)
			return
		if (i<31)
			initial_matrix = matrix(M.transform)
			initial_matrix.Translate(0,1)
			animate(M, transform = initial_matrix, time = 1, loop = 0)
		if (i>30)
			initial_matrix = matrix(M.transform)
			initial_matrix.Translate(0,-1)
			animate(M, transform = initial_matrix, time = 1, loop = 0)
		M.setDir(turn(M.dir, 90))
		switch (M.dir)
			if (NORTH)
				initial_matrix = matrix(M.transform)
				initial_matrix.Translate(0,3)
				animate(M, transform = initial_matrix, time = 1, loop = 0)
			if (SOUTH)
				initial_matrix = matrix(M.transform)
				initial_matrix.Translate(0,-3)
				animate(M, transform = initial_matrix, time = 1, loop = 0)
			if (EAST)
				initial_matrix = matrix(M.transform)
				initial_matrix.Translate(3,0)
				animate(M, transform = initial_matrix, time = 1, loop = 0)
			if (WEST)
				initial_matrix = matrix(M.transform)
				initial_matrix.Translate(-3,0)
				animate(M, transform = initial_matrix, time = 1, loop = 0)
		sleep(0.1 SECONDS)
	M.lying_fix()
	M.dancing = FALSE

//VOICE OF MADNESS
/datum/discipline_power/dementation/voice_of_madness
	name = "Voice of Madness"
	desc = "Your voice becomes a source of utter insanity, affecting you and all those around you."

	level = 4

	check_flags = DISC_CHECK_CAPABLE | DISC_CHECK_SPEAK
	target_type = TARGET_LIVING
	range = 7

	multi_activate = TRUE
	cooldown_length = 15 SECONDS
	duration_length = 3 SECONDS

/datum/discipline_power/dementation/voice_of_madness/activate(mob/living/target)
	. = ..()
	if(iscarbon(target))
		var/mob/living/carbon/carbon_target = target
		carbon_target.remove_overlay(MUTATIONS_LAYER)
		var/mutable_appearance/dementation_overlay1 = mutable_appearance('code/modules/wod13/icons.dmi', "dementation", -MUTATIONS_LAYER)
		dementation_overlay1.pixel_z = 1
		//what the fuck
		carbon_target.overlays_standing[MUTATIONS_LAYER] = dementation_overlay1
		carbon_target.apply_overlay(MUTATIONS_LAYER)
		new /datum/hallucination/death_malkavian(carbon_target, TRUE)

		var/mob/living/carbon/human/human_owner = owner // Под ифом чтобы не было приколов где ты попытался застанить мр дога и застанился ток сам)
		human_owner.remove_overlay(MUTATIONS_LAYER)
		var/mutable_appearance/dementation_overlay2 = mutable_appearance('code/modules/wod13/icons.dmi', "dementation", -MUTATIONS_LAYER)
		dementation_overlay2.pixel_z = 1
		human_owner.overlays_standing[MUTATIONS_LAYER] = dementation_overlay2
		human_owner.apply_overlay(MUTATIONS_LAYER)
		new /datum/hallucination/death_malkavian(human_owner, TRUE)

/datum/discipline_power/dementation/voice_of_madness/deactivate(mob/living/target)
	. = ..()
	if(iscarbon(target))
		var/mob/living/carbon/carbon_target = target
		carbon_target.remove_overlay(MUTATIONS_LAYER)
		var/mob/living/carbon/human/human_owner = owner
		human_owner.remove_overlay(MUTATIONS_LAYER)

//TOTAL INSANITY
/datum/discipline_power/dementation/total_insanity
	name = "Total Insanity"
	desc = "Bring out the darkest parts of a person's psyche, bringing them to utter insanity."

	level = 5

	check_flags = DISC_CHECK_CAPABLE | DISC_CHECK_SPEAK
	target_type = TARGET_LIVING
	range = 7

	multi_activate = TRUE
	cooldown_length = 15 SECONDS
	duration_length = 3 SECONDS

/datum/discipline_power/dementation/total_insanity/activate(mob/living/target)
	. = ..()
	if(iscarbon(target))
		var/mob/living/carbon/carbon_target = target
		carbon_target.remove_overlay(MUTATIONS_LAYER)
		var/mutable_appearance/dementation_overlay = mutable_appearance('code/modules/wod13/icons.dmi', "dementation", -MUTATIONS_LAYER)
		dementation_overlay.pixel_z = 1
		//what the fuck
		carbon_target.overlays_standing[MUTATIONS_LAYER] = dementation_overlay
		carbon_target.apply_overlay(MUTATIONS_LAYER)

	var/datum/cb = CALLBACK(target, /mob/living/proc/attack_myself_command)
	for(var/i in 1 to 10)
		addtimer(cb, (i - 1) * 1.5 SECONDS)

/datum/discipline_power/dementation/total_insanity/deactivate(mob/living/target)
	. = ..()
	if(iscarbon(target))
		var/mob/living/carbon/carbon_target = target
		carbon_target.remove_overlay(MUTATIONS_LAYER)
